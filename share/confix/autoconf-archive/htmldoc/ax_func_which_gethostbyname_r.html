<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01//EN"
"http://www.w3.org/TR/html4/strict.dtd">
<html lang="en">
 <head>
  <title>
   Autoconf Macro: ax_func_which_gethostbyname_r
  </title>
  <link rel="stylesheet" type="text/css" href="ac-archive.css">
 </head>
 <body>
  <table summary="web navigation" style="width:100%;">
   <tbody>
    <tr>
     <td style="width:50%;" align="center">
      <a href=
      "http://autoconf-archive.cryp.to/ax_func_which_gethostbyname_r.m4">Download
      M4 Source</a>
     </td>
     <td style="width:50%;" align="center">
      <a href="macros-by-category.html">Macro Index Page</a>
     </td>
    </tr>
   </tbody>
  </table>
  <hr>
  <h1>
   ax_func_which_gethostbyname_r
  </h1>
  <h2>
   Synopsis
  </h2>
  <p class="indent" style="white-space:nowrap;">
   <code>AX_FUNC_WHICH_GETHOSTBYNAME_R</code>
  </p>
  <h2>
   Description
  </h2>
  <div class="indent">
   <p>
    Determines which historical variant of the gethostbyname_r() call (taking
    three, five, or six arguments) is available on the system and defines one
    of the following macros accordingly:
   </p>
   <pre>
  HAVE_FUNC_GETHOSTBYNAME_R_6
  HAVE_FUNC_GETHOSTBYNAME_R_5
  HAVE_FUNC_GETHOSTBYNAME_R_3
</pre>
   <p>
    If used in conjunction with gethostname.c, the API demonstrated in test.c
    can be used regardless of which gethostbyname_r() is available. These
    example files can be found at
    http://www.csn.ul.ie/~caolan/publink/gethostbyname_r
   </p>
   <p>
    based on David Arnold's autoconf suggestion in the threads faq
   </p>
   <p>
    Originally named "AC_caolan_FUNC_WHICH_GETHOSTBYNAME_R". Rewritten for
    Autoconf 2.5x by Daniel Richard G.
   </p>
  </div>
  <h2>
   Author
  </h2>
  <p class="indent">
   Caolan McNamara &lt;caolan@skynet.ie&gt;, Daniel Richard G.
   &lt;skunk@iskunk.org&gt;
  </p>
  <h2>
   Last Modified
  </h2>
  <p class="indent">
   2005-01-21
  </p>
  <h2>
   M4 Source Code
  </h2>
  <div class="indent">
   <pre class="m4source">
AC_DEFUN([AX_FUNC_WHICH_GETHOSTBYNAME_R], [

    AC_LANG_PUSH(C)
    AC_MSG_CHECKING([how many arguments gethostbyname_r() takes])

    AC_CACHE_VAL(ac_cv_func_which_gethostbyname_r, [

################################################################

ac_cv_func_which_gethostbyname_r=unknown

#
# ONE ARGUMENT (sanity check)
#

# This should fail, as there is no variant of gethostbyname_r() that takes
# a single argument. If it actually compiles, then we can assume that
# netdb.h is not declaring the function, and the compiler is thereby
# assuming an implicit prototype. In which case, we're out of luck.
#
AC_COMPILE_IFELSE(
    AC_LANG_PROGRAM(
        [[#include &lt;netdb.h&gt;]],
        [[
            char *name = "www.gnu.org";
            (void)gethostbyname_r(name) /* ; */
        ]]),
    ac_cv_func_which_gethostbyname_r=no)

#
# SIX ARGUMENTS
# (e.g. Linux)
#

if test "$ac_cv_func_which_gethostbyname_r" = "unknown"; then

AC_COMPILE_IFELSE(
    AC_LANG_PROGRAM(
        [[#include &lt;netdb.h&gt;]],
        [[
            char *name = "www.gnu.org";
            struct hostent ret, *retp;
            char buf@&lt;:@1024@:&gt;@;
            int buflen = 1024;
            int my_h_errno;
            (void)gethostbyname_r(name, &amp;ret, buf, buflen, &amp;retp, &amp;my_h_errno) /* ; */
        ]]),
    ac_cv_func_which_gethostbyname_r=six)

fi

#
# FIVE ARGUMENTS
# (e.g. Solaris)
#

if test "$ac_cv_func_which_gethostbyname_r" = "unknown"; then

AC_COMPILE_IFELSE(
    AC_LANG_PROGRAM(
        [[#include &lt;netdb.h&gt;]],
        [[
            char *name = "www.gnu.org";
            struct hostent ret;
            char buf@&lt;:@1024@:&gt;@;
            int buflen = 1024;
            int my_h_errno;
            (void)gethostbyname_r(name, &amp;ret, buf, buflen, &amp;my_h_errno) /* ; */
        ]]),
    ac_cv_func_which_gethostbyname_r=five)

fi

#
# THREE ARGUMENTS
# (e.g. AIX, HP-UX, Tru64)
#

if test "$ac_cv_func_which_gethostbyname_r" = "unknown"; then

AC_COMPILE_IFELSE(
    AC_LANG_PROGRAM(
        [[#include &lt;netdb.h&gt;]],
        [[
            char *name = "www.gnu.org";
            struct hostent ret;
            struct hostent_data data;
            (void)gethostbyname_r(name, &amp;ret, &amp;data) /* ; */
        ]]),
    ac_cv_func_which_gethostbyname_r=three)

fi

################################################################

]) dnl end AC_CACHE_VAL

case "$ac_cv_func_which_gethostbyname_r" in
    three)
    AC_MSG_RESULT([three])
    AC_DEFINE(HAVE_FUNC_GETHOSTBYNAME_R_3)
    ;;

    five)
    AC_MSG_RESULT([five])
    AC_DEFINE(HAVE_FUNC_GETHOSTBYNAME_R_5)
    ;;

    six)
    AC_MSG_RESULT([six])
    AC_DEFINE(HAVE_FUNC_GETHOSTBYNAME_R_6)
    ;;

    no)
    AC_MSG_RESULT([cannot find function declaration in netdb.h])
    ;;

    unknown)
    AC_MSG_RESULT([can't tell])
    ;;

    *)
    AC_MSG_ERROR([internal error])
    ;;
esac

AC_LANG_POP(C)

]) dnl end AC_DEFUN
</pre>
  </div>
  <h2>
   License
  </h2>
  <div class="indent">
   <span style="white-space:nowrap;">Copyright &copy; 2005 Caolan McNamara
   &lt;caolan@skynet.ie&gt;</span><br>
   <span style="white-space:nowrap;">Copyright &copy; 2005 Daniel Richard G.
   &lt;skunk@iskunk.org&gt;</span>
   <p>
    This program is free software; you can redistribute it and/or modify it
    under the terms of the GNU General Public License as published by the Free
    Software Foundation; either version 2 of the License, or (at your option)
    any later version.
   </p>
   <p>
    This program is distributed in the hope that it will be useful, but WITHOUT
    ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
    FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for
    more details.
   </p>
   <p>
    You should have received a copy of the GNU General Public License along
    with this program; if not, write to the Free Software Foundation, Inc., 59
    Temple Place - Suite 330, Boston, MA 02111-1307, USA.
   </p>
   <p>
    As a special exception, the respective Autoconf Macro's copyright owner
    gives unlimited permission to copy, distribute and modify the configure
    scripts that are the output of Autoconf when processing the Macro. You need
    not follow the terms of the GNU General Public License when using or
    distributing such scripts, even though portions of the text of the Macro
    appear in them. The GNU General Public License (GPL) does govern all other
    use of the material that constitutes the Autoconf Macro.
   </p>
   <p>
    This special exception to the GPL applies to versions of the Autoconf Macro
    released by the Autoconf Macro Archive. When you make and distribute a
    modified version of the Autoconf Macro, you may extend this special
    exception to the GPL to apply to your modified version as well.
   </p>
  </div>
 </body>
</html>
