<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01//EN"
"http://www.w3.org/TR/html4/strict.dtd">
<html lang="en">
 <head>
  <title>
   Autoconf Macro: berkeley_db
  </title>
  <link rel="stylesheet" type="text/css" href="ac-archive.css">
 </head>
 <body>
  <table summary="web navigation" style="width:100%;">
   <tbody>
    <tr>
     <td style="width:50%;" align="center">
      <a href="http://autoconf-archive.cryp.to/berkeley_db.m4">Download M4
      Source</a>
     </td>
     <td style="width:50%;" align="center">
      <a href="macros-by-category.html">Macro Index Page</a>
     </td>
    </tr>
   </tbody>
  </table>
  <hr>
  <h1>
   berkeley_db
  </h1>
  <h2>
   Synopsis
  </h2>
  <p class="indent" style="white-space:nowrap;">
   <code>AX_BERKELEY_DB([MINIMUM-VERSION [, ACTION-IF-FOUND [,
   ACTION-IF-NOT-FOUND]]])</code>
  </p>
  <h2>
   Description
  </h2>
  <div class="indent">
   <p>
    This macro tries to find Berkeley DB. It honors MINIMUM-VERSION if given.
   </p>
   <p>
    If libdb is found, DB_HEADER and DB_LIBS variables are set and
    ACTION-IF-FOUND shell code is executed if specified. DB_HEADER is set to
    location of db.h header in quotes (e.g. "db3/db.h") and AC_DEFINE_UNQUOTED
    is called on it, so that you can type
   </p>
   <pre>
     #include DB_HEADER
</pre>
   <p>
    in your C/C++ code. DB_LIBS is set to linker flags needed to link against
    the library (e.g. -ldb3.1) and AC_SUBST is called on it.
   </p>
  </div>
  <h2>
   Author
  </h2>
  <p class="indent">
   Vaclav Slavik &lt;vaclav.slavik@matfyz.cz&gt;
  </p>
  <h2>
   Last Modified
  </h2>
  <p class="indent">
   2005-01-21
  </p>
  <h2>
   M4 Source Code
  </h2>
  <div class="indent">
   <pre class="m4source">
AC_DEFUN([AX_BERKELEY_DB],
[
  old_LIBS="$LIBS"

  minversion=ifelse([$1], ,,$1)

  DB_HEADER=""
  DB_LIBS=""

  if test -z $minversion ; then
      minvermajor=0
      minverminor=0
      minverpatch=0
      AC_MSG_CHECKING([for Berkeley DB])
  else
      minvermajor=`echo $minversion | cut -d. -f1`
      minverminor=`echo $minversion | cut -d. -f2`
      minverpatch=`echo $minversion | cut -d. -f3`
      minvermajor=${minvermajor:-0}
      minverminor=${minverminor:-0}
      minverpatch=${minverpatch:-0}
      AC_MSG_CHECKING([for Berkeley DB &gt;= $minversion])
  fi

  for version in "" 5.0 4.9 4.8 4.7 4.6 4.5 4.4 4.3 4.2 4.1 4.0 3.6 3.5 3.4 3.3 3.2 3.1 ; do

    if test -z $version ; then
        db_lib="-ldb"
        try_headers="db.h"
    else
        db_lib="-ldb-$version"
        try_headers="db$version/db.h db`echo $version | sed -e 's,\..*,,g'`/db.h"
    fi

    LIBS="$old_LIBS $db_lib"

    for db_hdr in $try_headers ; do
        if test -z $DB_HEADER ; then
            AC_LINK_IFELSE(
                [AC_LANG_PROGRAM(
                    [
                        #include &lt;${db_hdr}&gt;
                    ],
                    [
                        #if !((DB_VERSION_MAJOR &gt; (${minvermajor}) || \
                              (DB_VERSION_MAJOR == (${minvermajor}) &amp;&amp; \
                                    DB_VERSION_MINOR &gt; (${minverminor})) || \
                              (DB_VERSION_MAJOR == (${minvermajor}) &amp;&amp; \
                                    DB_VERSION_MINOR == (${minverminor}) &amp;&amp; \
                                    DB_VERSION_PATCH &gt;= (${minverpatch}))))
                            #error "too old version"
                        #endif

                        DB *db;
                        db_create(&amp;db, NULL, 0);
                    ])],
                [
                    AC_MSG_RESULT([header $db_hdr, library $db_lib])

                    DB_HEADER="$db_hdr"
                    DB_LIBS="$db_lib"
                ])
        fi
    done
  done

  LIBS="$old_LIBS"

  if test -z $DB_HEADER ; then
    AC_MSG_RESULT([not found])
    ifelse([$3], , :, [$3])
  else
    AC_DEFINE_UNQUOTED(DB_HEADER, ["$DB_HEADER"])
    AC_SUBST(DB_LIBS)
    ifelse([$2], , :, [$2])
  fi
])
</pre>
  </div>
  <h2>
   License
  </h2>
  <div class="indent">
   <span style="white-space:nowrap;">Copyright &copy; 2005 Vaclav Slavik
   &lt;vaclav.slavik@matfyz.cz&gt;</span>
   <p>
    This program is free software; you can redistribute it and/or modify it
    under the terms of the GNU General Public License as published by the Free
    Software Foundation; either version 2 of the License, or (at your option)
    any later version.
   </p>
   <p>
    This program is distributed in the hope that it will be useful, but WITHOUT
    ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
    FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for
    more details.
   </p>
   <p>
    You should have received a copy of the GNU General Public License along
    with this program; if not, write to the Free Software Foundation, Inc., 59
    Temple Place - Suite 330, Boston, MA 02111-1307, USA.
   </p>
   <p>
    As a special exception, the respective Autoconf Macro's copyright owner
    gives unlimited permission to copy, distribute and modify the configure
    scripts that are the output of Autoconf when processing the Macro. You need
    not follow the terms of the GNU General Public License when using or
    distributing such scripts, even though portions of the text of the Macro
    appear in them. The GNU General Public License (GPL) does govern all other
    use of the material that constitutes the Autoconf Macro.
   </p>
   <p>
    This special exception to the GPL applies to versions of the Autoconf Macro
    released by the Autoconf Macro Archive. When you make and distribute a
    modified version of the Autoconf Macro, you may extend this special
    exception to the GPL to apply to your modified version as well.
   </p>
  </div>
 </body>
</html>
